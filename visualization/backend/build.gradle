import org.apache.commons.lang3.RandomStringUtils

buildscript {
    repositories {
        for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
            maven { url(mavenRepositoryUrl) }
        }
    }
    dependencies {
        classpath project.rootProject.ext.jarDependency("org.apache.commons", "commons-lang3")
    }
}
plugins {
    id 'java'
    id 'org.inferred.processors' version '3.3.0'
    id "org.springframework.boot" version "2.2.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

group = project.rootProject.ext.constructGroupName("", project.getName())
version = project.rootProject.constructVersion()
sourceCompatibility = project.rootProject.ext.javaVersion
targetCompatibility = project.rootProject.ext.javaVersion

repositories {
    for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
        maven { url(mavenRepositoryUrl) }
    }
}

dependencies {
    processor 'org.inferred:freebuilder:2.6.1'
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation(project.rootProject.ext.jarDependency("com.google.guava", "guava"))
    implementation(project.rootProject.ext.jarDependency("org.apache.commons", "commons-lang3"))
    testImplementation(project.rootProject.ext.jarDependency("junit", "junit"))
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
}
test {
    useJUnit()
}

def jarBaseName = project.rootProject.ext.visualizationBackendJarBaseName
def jarPath = project.rootProject.ext.visualizationBackendJarPath
bootJar {
    baseName(jarBaseName)
}
task runWithJar(type: Exec) {
    commandLine(
            "java",
            "-jar", jarPath,
    )
    doFirst {
        println("running command: ${String.join(" ", getCommandLine())}")
        println("access with chrome: http://localhost:8080/api/greeting?username=${RandomStringUtils.randomAlphabetic(8)}")
    }
    dependsOn(bootJar)
}

/********************** build docker images **********************/
def buildDockerPath = "${project.buildDir}/docker/"
def httpProxy = java.util.Optional.ofNullable(System.getenv("http_proxy")).orElse("")
def httpsProxy = java.util.Optional.ofNullable(System.getenv("https_proxy")).orElse("")
