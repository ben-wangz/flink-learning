buildscript {
    repositories {
        for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
            maven { url(mavenRepositoryUrl) }
        }
    }
}
plugins {
    id 'java'
}

group = project.rootProject.ext.constructGroupName("", project.getName())
version = project.rootProject.constructVersion()
sourceCompatibility = project.rootProject.ext.javaVersion
targetCompatibility = project.rootProject.ext.javaVersion

repositories {
    for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
        maven { url(mavenRepositoryUrl) }
    }
}

def scalaBinaryVersion = project.rootProject.ext.scalaBinaryVersion
dependencies {
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-core"))
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-java"))
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-scala_${scalaBinaryVersion}"))
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-clients_${scalaBinaryVersion}"))
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-connector-kafka_2.11"))
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-table-api-java-bridge_2.11"))
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-table-planner-blink_2.11"))
    compileOnly(project.rootProject.ext.jarDependency("org.apache.flink", "flink-streaming-scala_2.11"))
    implementation(project.rootProject.ext.jarDependency("org.apache.httpcomponents", "httpclient"))
    implementation(project.rootProject.ext.jarDependency("org.slf4j", "slf4j-api"))
    implementation(project.rootProject.ext.jarDependency("org.slf4j", "slf4j-simple"))
    testImplementation(project.rootProject.ext.jarDependency("junit", "junit"))
}
test {
    useJUnit()
}

def mainClass = "flink.learning.example.meteorological_data.Entrypoint"
def jarBaseName = project.rootProject.ext.visualizationMeteorologicalDataJarBaseName
def jarPath = project.rootProject.ext.visualizationMeteorologicalDataJarPath
jar {
    manifest {
        attributes "Main-Class": mainClass
    }
    setArchivesBaseName(jarBaseName)
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def dependencyJarPath = "${project.buildDir}/dependencies"
task copyDependencies(type: Sync) {
    from configurations.compileClasspath
    include "**/*"
    into dependencyJarPath
}
task runMeteorologicalData(type: Exec) {
    executable("java")
    args(
            "-classpath", "${dependencyJarPath}/*:${jarPath}",
            mainClass,
            "regular",
    )
    doFirst {
        println("running command: ${String.join(" ", getCommandLine())}")
    }
    dependsOn(jar)
    dependsOn(copyDependencies)
}
task runMeteorologicalDataWithTable(type: Exec) {
    executable("java")
    args(
            "-classpath", "${dependencyJarPath}/*:${jarPath}",
            mainClass,
            "table",
    )
    doFirst {
        println("running command: ${String.join(" ", getCommandLine())}")
    }
    dependsOn(jar)
    dependsOn(copyDependencies)
}
